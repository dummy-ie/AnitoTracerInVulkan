#version 460
#extension GL_EXT_nonuniform_qualifier : require
#extension GL_GOOGLE_include_directive : require
#extension GL_EXT_ray_tracing : require
#include "Material.glsl"

layout(binding = 4) readonly buffer VertexArray { float Vertices[]; };
layout(binding = 5) readonly buffer IndexArray { uint Indices[]; };
layout(binding = 6) readonly buffer MaterialArray { Material[] Materials; };
layout(binding = 7) readonly buffer LightsArray { LightProperties[] Lights; }; 
layout(binding = 8) readonly buffer OffsetArray { uvec2[] Offsets; };
layout(binding = 9) uniform sampler2D[] TextureSamplers;

#include "Scatter.glsl"
#include "Vertex.glsl"

hitAttributeEXT vec2 HitAttributes;
rayPayloadInEXT RayPayload Ray;

vec2 Mix(vec2 a, vec2 b, vec2 c, vec3 barycentrics)
{
	return a * barycentrics.x + b * barycentrics.y + c * barycentrics.z;
}

vec3 Mix(vec3 a, vec3 b, vec3 c, vec3 barycentrics) 
{
    return a * barycentrics.x + b * barycentrics.y + c * barycentrics.z;
}

vec3 calculatePointLight(LightProperties pl, vec3 worldPos, vec3 normal) 
{
	// Computing the coordinates of the hit position
	const vec3 worldNrm = normalize(transpose(inverse(mat3(gl_ObjectToWorldEXT))) * normal);

	// Compute the diffuse light.
	vec3 lightDir = pl.LightPos.xyz - worldPos;
	float attenuation = 1.0 / dot(lightDir, lightDir);

	// Compute the light colors and intensity.
	vec3 lightCol = pl.LightColor.xyz * pl.LightColor.w * attenuation;
	vec3 ambientLight = pl.AmbientColor.xyz * pl.AmbientColor.w;
	vec3 diffuseLight = lightCol * max(dot(worldNrm, normalize(lightDir)), 0);
	
	vec3 lighting = diffuseLight + ambientLight;
	 
	return lighting;
}
 
vec3 calculateDirectionalLight(LightProperties dl, vec3 worldPos, vec3 normal) 
{
	vec3 worldNrm = normalize(transpose(inverse(mat3(gl_ObjectToWorldEXT))) * normal);
	vec3 lightDir = -normalize(dl.LightPos);

	vec3 ambientColor = dl.AmbientColor.rgb * dl.AmbientColor.w;

    float diffuseFactor = max(dot(worldNrm, lightDir), 0.f);
    vec3 diffuseColor = dl.LightColor.rgb * dl.LightColor.w * diffuseFactor;
	 
	vec3 lighting = (ambientColor + diffuseColor) * 0.2;
	return lighting;
} 

vec3 calculateSpotLight(LightProperties sl, vec3 worldPos, vec3 normal) 
{         
	float cutoff = cos(radians(90.0)); // Convert degrees to radians and compute cosine
	vec3 lightDir = normalize(sl.LightPos.xyz - worldPos); // Direction from light to hit point
	vec3 lightDirection = vec3(0, -1, 0);
	vec3 spotDir = normalize(lightDirection); // Direction of the spot light 
	float spotFactor = dot(lightDir, -spotDir); // Cosine of the angle between lightDir and spotDir
	 
	if (spotFactor > cutoff) {
		vec3 lighting = calculatePointLight(sl, worldPos, normal);
		return lighting * (1.0 - (1.0 - spotFactor) * 1.0 / (1.0 - cutoff));
	}
	return vec3(0.0); // Return no light if outside the spot light cone
}

void main()
{
	// Get the material.
	const uvec2 offsets = Offsets[gl_InstanceCustomIndexEXT];
	const uint indexOffset = offsets.x;
	const uint vertexOffset = offsets.y;
	const Vertex v0 = UnpackVertex(vertexOffset + Indices[indexOffset + gl_PrimitiveID * 3 + 0]);
	const Vertex v1 = UnpackVertex(vertexOffset + Indices[indexOffset + gl_PrimitiveID * 3 + 1]);
	const Vertex v2 = UnpackVertex(vertexOffset + Indices[indexOffset + gl_PrimitiveID * 3 + 2]);
	const Material material = Materials[v0.MaterialIndex];
	  
	// Compute the ray hit point properties.	
	const vec3 barycentrics = vec3(1.0 - HitAttributes.x - HitAttributes.y, HitAttributes.x, HitAttributes.y);
	const vec3 normal = normalize(Mix(v0.Normal, v1.Normal, v2.Normal, barycentrics));
	const vec2 texCoord = Mix(v0.TexCoord, v1.TexCoord, v2.TexCoord, barycentrics);

	// For lighting computations.
	const vec3 pos = Mix(v0.Position, v1.Position, v2.Position, barycentrics);
	const vec3 worldPos = vec3(gl_ObjectToWorldEXT * vec4(pos, 1.0));  // Transforming the position to world space
	 
	vec3 lighting = vec3(0);
	 
	if (Lights.length() == 0) { // Pink light if buffer is empty
		//LightProperties dl = InitializeTestDLProperties(); // Adding directional light.
		//lighting += calculateDirectionalLight(dl, worldPos, normal);
		//LightProperties pl = InitializeTestPLProperties(); // Adding point light.
		//lighting += calculatePointLight(pl, worldPos, normal);
		//LightProperties sl = InitializeTestSLProperties(); // Adding spot light.
		//lighting += calculateSpotLight(sl, worldPos, normal);
	} else {
		for (int i = 0; i < Lights.length(); i++) {
			if (Lights[i].LightType == PointLight) { // Point Light
				lighting += calculatePointLight(Lights[i], worldPos, normal);
			} else if (Lights[i].LightType == DirectionalLight) { // Directional Light
				lighting += calculateDirectionalLight(Lights[i], worldPos, normal);
			} else if (Lights[i].LightType == SpotLight) { // Spot Light
				//lighting += calculateSpotLight(Lights[i], worldPos, normal);
			}
		}
	}
	
	Ray = Scatter(material, gl_WorldRayDirectionEXT, normal, texCoord, gl_HitTEXT, Ray.RandomSeed, lighting);
}
	